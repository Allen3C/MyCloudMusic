//应用Android插件
apply plugin: 'com.android.application'
//sharesdk
apply plugin: 'com.mob.sdk'

android {
    //配置不同的环境
    productFlavors {
        //开发环境
        dev {
            //API端点
            buildConfigField('String', "ENDPOINT", '"http://dev-my-cloud-music-api-rails.ixuea.com/"')

            //资源短点
            buildConfigField 'String', 'RESOURCE_ENDPOINT', '"http://dev-cources-music.ixuea.com/%s"'

            dimension = minSdkVersion
        }

        //正式环境(实际没有)
        prod {
            //API端点
            buildConfigField 'String', 'ENDPOINT', '"http://api-courses-misuc.ixuea.com/"'

            //资源短点
            buildConfigField 'String', 'RESOURCE_ENDPOINT', '"http://cources-music.ixuea.com/%s"'

            dimension = minSdkVersion
        }
    }
    //将源码兼容版本设置为Java 8
    //因为Butterknife需要Java 8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    //编译版本
    compileSdkVersion rootProject.ext.compileSdkVersion
    //编译工具版本
    buildToolsVersion rootProject.ext.buildToolsVersion

    //默认配置
    defaultConfig {
        //应用唯一标识
        applicationId "com.example.mycloudmusic"
        //最小版本
        minSdkVersion rootProject.ext.minSdkVersion
        //目标版本
        targetSdkVersion rootProject.ext.targetSdkVersion
        //版本号
        versionCode rootProject.ext.versionCode
        //版本名称
        versionName rootProject.ext.versionName
        //开启MultiDex
        multiDexEnabled true
        //测试相关
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //渠道相关
        flavorDimensions "minSdkVersion"
    }

    //签名配置
    //要写到使用签名配置的前面
    signingConfigs {
        //以下两个版本都用的是同一个
        //真实项目中可能会用多个
        debug {
            //签名文件路径
            //根目录config/mycloudmusic_cources.jks
            storeFile file('../config/mycloudmusic_cources.jks')
            //key密码
            storePassword "19941026121x"

            //别名
            keyAlias "ch1"

            //签名密码
            keyPassword "19941026121x1"
        }

        //正式版配置
        release {
            storeFile file('../config/mycloudmusic_cources.jks')
            storePassword "19941026121x"
            keyAlias "ch1"
            keyPassword "19941026121x1"
        }
    }


    buildTypes {

        //调试版本
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            //使用签名配置
            signingConfig signingConfigs.debug
        }

        //正式版本
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            //使用签名配置
            signingConfig signingConfigs.release
        }
    }

}

//sharesdk配置
MobSDK {
    appKey "2f689abd9b948"
    appSecret "73f94dd73bf6708279512734a635b61f"

    //第三方平台配置
    ShareSDK {
        devInfo {
            QQ {
                id 1
                sortId 1
                appId "101880638"
                appKey "6302dec5ffa671f39bffea901d67a71d"
                shareByAppClient true
                bypassApproval false
                enable true
            }
            SinaWeibo {
                id 2
                sortId 2
                appKey "1016702613"
                appSecret "898cb1577f3eedcd5e455065a8929fb0"
                callbackUri "http://www.ixuea.com"
                shareByAppClient true
                enable true
            }

        }
    }

}

dependencies {
    //添加所有libs目录里面的jar包
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //添加appcompat包
    implementation 'androidx.appcompat:appcompat:1.1.0'
    //引用constraintlayout依赖
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    //Java测试依赖
    testImplementation 'junit:junit:4.12'
    //Android测试依赖
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    //圆形指示器
    implementation 'me.relex:circleindicator:2.1.4'
    //butterknife
    implementation 'com.jakewharton:butterknife:10.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'
    //material design依赖
    implementation 'com.google.android.material:material:1.0.0'
    //apache common lang3工具包
    implementation 'org.apache.commons:commons-lang3:3.9'
    //toast工具类
    implementation 'com.github.GrenderG:Toasty:1.4.2'
    //MultiDex
    implementation "androidx.multidex:multidex:2.0.0"

    //请求网络相关
    //okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.2.0'
    //用来打印okhttp请求日志
    //也可以自定义
    implementation("com.squareup.okhttp3:logging-interceptor:4.2.0")
    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    //使用gson解析json
    implementation 'com.google.code.gson:gson:2.8.5'
    //适配retrofit使用gson解析
    //版本要和retrofit一样
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    //适配retrofit支持rxjava
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.2'
    //使用了Android响应式编程
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    //end 请求网络相关

    //通过OkHttp的拦截器机制
    //实现在Chrome中抓包
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    //通过OkHttp的拦截器机制
    //实现在应用内显示网络请求功能
    debugImplementation "com.github.ChuckerTeam.Chucker:library:3.0.1"
    releaseImplementation "com.github.ChuckerTeam.Chucker:library-no-op:3.0.1"

    //跨界面通信框架
    implementation 'org.greenrobot:eventbus:3.1.1'

    //图片加载框架glide
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.10.0'
}
